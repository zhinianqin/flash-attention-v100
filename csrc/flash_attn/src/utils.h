/******************************************************************************
 * Copyright (c) 2023, Tri Dao.
 ******************************************************************************/

#pragma once

#include <assert.h>
#include <stdint.h>
#include <stdlib.h>

#include <cuda_fp16.h>

#if defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= 800
#include <cuda_bf16.h>
#endif

#include <cute/tensor.hpp>

#include <cutlass/array.h>
#include <cutlass/cutlass.h>
#include <cutlass/numeric_conversion.h>
#include <cutlass/numeric_types.h>

#include "namespace_config.h"

////////////////////////////////////////////////////////////////////////////////////////////////////

namespace FLASH_NAMESPACE {

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename T>
__forceinline__ __device__ uint32_t relu2(const uint32_t x);

template<>
__forceinline__ __device__ uint32_t relu2<cutlass::half_t>(const uint32_t x) {
    uint32_t res;
    const uint32_t zero = 0u;
#if defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= 800
    asm volatile("max.f16x2 %0, %1, %2;\n" : "=r"(res) : "r"(x), "r"(zero));
#else
    asm volatile( \
        "{\n" \
        "\t .reg .f16x2 sela;\n" \
        "\t set.gtu.u32.f16x2 sela, %1, %2;\n" \
        "\t and.b32 %0, sela, %1;\n" 
        "}\n" : "=r"(res) : "r"(x), "r"(zero));
#endif
    return res;
}

#if defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= 800
template<>
__forceinline__ __device__ uint32_t relu2<cutlass::bfloat16_t>(const uint32_t x) {
    uint32_t res;
    const uint32_t zero = 0u;
    asm volatile("max.bf16x2 %0, %1, %2;\n" : "=r"(res) : "r"(x), "r"(zero));
    return res;
}
#endif

////////////////////////////////////////////////////////////////////////////////////////////////////

#if defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= 800

template<typename T>
__forceinline__ __device__ uint32_t convert_relu2(const float2 x);

template<>
__forceinline__ __device__ uint32_t convert_relu2<cutlass::half_t>(const float2 x) {
    uint32_t res;
    const uint32_t a = reinterpret_cast<const uint32_t&>(x.x);
    const uint32_t b = reinterpret_cast<const uint32_t&>(x.y);
    asm volatile("cvt.rn.relu.f16x2.f32 %0, %1, %2;\n" : "=r"(res) : "r"(b), "r"(a));
    return res;
}

template<>
__forceinline__ __device__ uint32_t convert_relu2<cutlass::bfloat16_t>(const float2 x) {
    uint32_t res;
    const uint32_t a = reinterpret_cast<const uint32_t&>(x.x);
    const uint32_t b = reinterpret_cast<const uint32_t&>(x.y);
    asm volatile("cvt.rn.relu.bf16x2.f32 %0, %1, %2;\n" : "=r"(res) : "r"(b), "r"(a));
    return res;
}

#endif

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename T>
struct MaxOp {
__device__ __forceinline__ T operator()(T const & x, T const & y) { return x > y ? x : y; }
};

template <>
struct MaxOp<float> {
// This is slightly faster
__device__ __forceinline__ float operator()(float const &x, float const &y) { return max(x, y); }
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename T>
struct SumOp {
__device__ __forceinline__ T operator()(T const & x, T const & y) { return x + y; }
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template<int THREADS>
struct Allreduce {
    static_assert(THREADS == 32 || THREADS == 16 || THREADS == 8 || THREADS == 4);
    template<typename T, typename Operator>
    static __device__ __forceinline__ T run(T x, Operator &op) {
        constexpr int OFFSET = THREADS / 2;
        x = op(x, __shfl_xor_sync(uint32_t(-1), x, OFFSET));
        return Allreduce<OFFSET>::run(x, op);
    }
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template<>
struct Allreduce<2> {
template<typename T, typename Operator> 
static __device__ __forceinline__ T run(T x, Operator &op) {
    x = op(x, __shfl_xor_sync(uint32_t(-1), x, 1));
    return x;
}
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template<bool A_in_regs=false, bool B_in_regs=false, typename Tensor0, typename Tensor1,
         typename Tensor2, typename Tensor3, typename Tensor4,
         typename TiledMma, typename TiledCopyA, typename TiledCopyB,
         typename ThrCopyA, typename ThrCopyB>
__forceinline__ __device__ void gemm(Tensor0 &acc, Tensor1 &tCrA, Tensor2 &tCrB, Tensor3 const& tCsA,
                            Tensor4 const& tCsB, TiledMma tiled_mma,
                            TiledCopyA smem_tiled_copy_A, TiledCopyB smem_tiled_copy_B,
                            ThrCopyA smem_thr_copy_A, ThrCopyB smem_thr_copy_B) {
    CUTE_STATIC_ASSERT_V(size<1>(tCrA) == size<1>(acc));                     // MMA_M
    CUTE_STATIC_ASSERT_V(size<1>(tCrB) == size<2>(acc));                     // MMA_N
    CUTE_STATIC_ASSERT_V(size<2>(tCrA) == size<2>(tCrB));                     // MMA_K
    Tensor tCrA_copy_view = smem_thr_copy_A.retile_D(tCrA);
    CUTE_STATIC_ASSERT_V(size<1>(tCsA) == size<1>(tCrA_copy_view));            // M
    Tensor tCrB_copy_view = smem_thr_copy_B.retile_D(tCrB);
    CUTE_STATIC_ASSERT_V(size<1>(tCsB) == size<1>(tCrB_copy_view));            // N
    if (!A_in_regs) { cute::copy(smem_tiled_copy_A, tCsA(_, _, _0{}), tCrA_copy_view(_, _, _0{})); }
    if (!B_in_regs) { cute::copy(smem_tiled_copy_B, tCsB(_, _, _0{}), tCrB_copy_view(_, _, _0{})); }
    #pragma unroll
    for (int i = 0; i < size<2>(tCrA); ++i) {
        if (i < size<2>(tCrA) - 1) {
            if (!A_in_regs) { cute::copy(smem_tiled_copy_A, tCsA(_, _, i + 1), tCrA_copy_view(_, _, i + 1)); }
            if (!B_in_regs) { cute::copy(smem_tiled_copy_B, tCsB(_, _, i + 1), tCrB_copy_view(_, _, i + 1)); }
        }
        cute::gemm(tiled_mma, tCrA(_, _, i), tCrB(_, _, i), acc);
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename Tensor0, typename Tensor1, typename Tensor2, typename Tensor3,
         typename TiledMma, typename TiledCopy, typename ThrCopy>
__forceinline__ __device__ void gemm_rs(Tensor0 &acc, Tensor1 &tCrA, Tensor2 &tCrB, Tensor3 const& tCsB,
                               TiledMma tiled_mma, TiledCopy smem_tiled_copy_B,
                               ThrCopy smem_thr_copy_B) {
    CUTE_STATIC_ASSERT_V(size<1>(tCrA) == size<1>(acc));                     // MMA_M
    CUTE_STATIC_ASSERT_V(size<1>(tCrB) == size<2>(acc));                     // MMA_N
    CUTE_STATIC_ASSERT_V(size<2>(tCrA) == size<2>(tCrB));                     // MMA_K
    Tensor tCrB_copy_view = smem_thr_copy_B.retile_D(tCrB);
    CUTE_STATIC_ASSERT_V(size<1>(tCsB) == size<1>(tCrB_copy_view));            // N
    cute::copy(smem_tiled_copy_B, tCsB(_, _, _0{}), tCrB_copy_view(_, _, _0{}));
    #pragma unroll
    for (int i = 0; i < size<2>(tCrA); ++i) {
        if (i < size<2>(tCrA) - 1) {
            cute::copy(smem_tiled_copy_B, tCsB(_, _, i + 1), tCrB_copy_view(_, _, i + 1));
        }
        cute::gemm(tiled_mma, tCrA(_, _, i), tCrB(_, _, i), acc);
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// 因为编译器偶尔会重排指令，导致计算结果不对，所以独立重构了一个pv_gemm_rs
template <typename Tensor0, typename Tensor1, typename Tensor2, typename Tensor3,
          typename TiledMma, typename TiledCopy, typename ThrCopy>
__forceinline__ __device__ void pv_gemm_rs(
    Tensor0 &acc,                   // 累加器 (Fragment)
    Tensor1 &tCrA,                 // P 矩阵 (Fragment)
    Tensor2 &tCrB,                   // V 矩阵在寄存器中的 View (Fragment)
    Tensor3 const& tCsB,             // V 矩阵在共享内存中的 View
    TiledMma tiled_mma,              // Tiled MMA 实例
    TiledCopy smem_tiled_copy_B,     // Tiled Copy 实例
    ThrCopy smem_thr_copy_B          // Thread-level Copy 实例
) {
    Tensor tCrB_copy_view = smem_thr_copy_B.retile_D(tCrB);
    constexpr int kFragK = decltype(size<2>(tCrA))::value;
    constexpr int kSrcK = decltype(size<2>(tCsB))::value;
    constexpr int kCopyK = decltype(size<2>(tCrB_copy_view))::value;
    static_assert(kFragK % kSrcK == 0, "pv_gemm_rs K/src mismatch");
    static_assert(kFragK % kCopyK == 0, "pv_gemm_rs K/copy mismatch");
    constexpr int kStepSrc = kFragK / kSrcK;
    constexpr int kStepCopy = kFragK / kCopyK;
    #pragma unroll
    for (int i = 0; i < kFragK; ++i) {
        if (i % kStepCopy == 0) {
            const int ck_copy = i / kStepCopy;
            const int ck_src = i / kStepSrc;
            cute::copy(smem_tiled_copy_B, tCsB(_, _, ck_src), tCrB_copy_view(_, _, ck_copy));
        }
        asm volatile("" : "+r"(i));
        //asm volatile("" : "+r"(kStepSrc));
        //asm volatile("" : "+r"(kStepCopy));
        cute::gemm(tiled_mma, tCrA(_, _, i), tCrB(_, _, i), acc);
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// For V100 (SM70), acc_layout is naturally ((2, 2, 2), MMA_M, MMA_N).
// We want to convert it to ((2, MMA_M), (2, 2, MMA_N))
template<typename Layout>
__forceinline__ __device__ auto convert_layout_acc_rowcol(Layout acc_layout) {
    static_assert(decltype(size<0>(acc_layout))::value == 8, "V100 requires 8 fragments per thread");
    static_assert(decltype(rank(acc_layout))::value == 3, "Layout must be 3D");
    // acc_layout shape: ((2, 2, 2), MMA_M, MMA_N)
    // Strides:           (1, 2, 4)
    // 我们希望列维度是平铺的 3 个维度： (Frag0, Frag2, MMA_N)
    // 使用单个 make_layout 包裹所有参数即可实现平铺
    return make_layout(
        // Rows: Fragment dim 1 (stride 2) + MMA_M
        make_layout(get<0, 1>(acc_layout), get<1>(acc_layout)), 
        // Cols: Fragment dim 0 (stride 1) + Fragment dim 2 (stride 4) + MMA_N
        // 修正：不要嵌套 make_layout，直接平铺参数
        make_layout(get<0, 0>(acc_layout), get<0, 2>(acc_layout), get<2>(acc_layout))
    );
};

////////////////////////////////////////////////////////////////////////////////////////////////////

// For V100 (SM70), the accumulator of the first GEMM (QK^T) has 8 fragments,
// which perfectly matches the required input size for the A-operand of the 
// second GEMM (P*V). No dimension re-tiling or "borrowing" from MMA_N is needed.
template<typename MMA_traits, typename Layout>
__forceinline__ __device__ auto convert_layout_acc_Aregs(Layout acc_layout) {
    static_assert(decltype(cute::size<0>(acc_layout))::value == 8, "V100 requires 8 fragments per thread");
    auto l_mode0 = cute::logical_divide(cute::get<0>(acc_layout), cute::Int<4>{});

    return cute::make_layout(
        cute::get<0>(l_mode0),
        cute::get<1>(acc_layout),
        cute::make_layout(cute::get<1>(l_mode0), cute::get<2>(acc_layout))
    );
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename Kernel_traits, typename ThrMma, typename TensorSP, typename TensorRP, typename ThrCopyA>
__forceinline__ __device__ auto convert_layout_C_to_A(
    const ThrMma& thr_mma,
    const TensorSP& p_layout_warp,
    const TensorRP& rP,
    ThrCopyA smem_thr_copy_P,
    const int lane_id
) {
    static_assert(Kernel_traits::kMmaThreads == 32,
                  "SM70 C->A register conversion requires single-warp MMA groups");
    static_assert(Kernel_traits::kWarpRows == 8,
                  "SM70 C->A register conversion requires kWarpRows == 8");
    static_assert(Kernel_traits::kBlockN == 32 || Kernel_traits::kBlockN == 64,
                  "SM70 C->A register conversion requires kBlockN == 32 or 64");
    static_assert(decltype(size(rP))::value == 8 || decltype(size(rP))::value == 16,
                  "Unexpected rP fragment size for SM70 register C->A conversion");

    auto tOrP = thr_mma.partition_fragment_A(p_layout_warp);
    auto tOrP_copy_view = smem_thr_copy_P.retile_D(tOrP);
    const int lane_group = lane_id & 0x11;
    const bool select_hi = (lane_id & 0x2) != 0;

    #pragma unroll
    for (int j = 0; j < size(tOrP_copy_view); ++j) {
        const int src_lane =
            lane_group |
            (((j >> 1) & 0x1) << 1) |
            (((j >> 3) & 0x3) << 2);
        const int base_idx =
            (j & 0x1) |
            (((j >> 2) & 0x1) << 2) |
            (((j >> 5) & 0x1) << 3);
        const float src_lo = static_cast<float>(rP(base_idx + 0));
        const float src_hi = static_cast<float>(rP(base_idx + 2));
        const float got_lo = __shfl_sync(0xffffffffu, src_lo, src_lane);
        const float got_hi = __shfl_sync(0xffffffffu, src_hi, src_lane);
        tOrP_copy_view(j) = static_cast<typename Kernel_traits::Element>(
            select_hi ? got_hi : got_lo
        );
    }

    return tOrP;
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template<typename Kernel_traits, typename ThrMma, typename TensorSP, typename TensorRP, typename ThrCopyA>
__forceinline__ __device__ auto convert_layout_C_to_A_v2(
    const ThrMma& thr_mma,
    const TensorSP& p_layout_warp,
    const TensorRP& rP,
    ThrCopyA smem_thr_copy_P,
    const int lane_id
) {
    static_assert(Kernel_traits::kMmaThreads == 32,
                  "SM70 C->A register conversion requires single-warp MMA groups");
    static_assert(Kernel_traits::kWarpRows == 8,
                  "SM70 C->A register conversion requires kWarpRows == 8");
    static_assert(Kernel_traits::kBlockN == 32 || Kernel_traits::kBlockN == 64,
                  "SM70 C->A register conversion requires kBlockN == 32 or 64");
    static_assert(decltype(size(rP))::value == 8 || decltype(size(rP))::value == 16,
                  "Unexpected rP fragment size for SM70 register C->A conversion");

    auto tOrP = thr_mma.partition_fragment_A(p_layout_warp);
    auto tOrP_copy_view = smem_thr_copy_P.retile_D(tOrP);
    const int lane_group = lane_id & 0x11;
    const bool select_hi = (lane_id & 0x2) != 0;

    using Element = typename Kernel_traits::Element;

    #pragma unroll
    for (int j = 0; j < size(tOrP_copy_view); ++j) {
        const int src_lane =
            lane_group |
            (((j >> 1) & 0x1) << 1) |
            (((j >> 3) & 0x3) << 2);
        const int base_idx =
            (j & 0x1) |
            (((j >> 2) & 0x1) << 2) |
            (((j >> 5) & 0x1) << 3);

        if constexpr (sizeof(Element) == 2) {
            Element el_lo = static_cast<Element>(static_cast<float>(rP(base_idx + 0)));
            Element el_hi = static_cast<Element>(static_cast<float>(rP(base_idx + 2)));

            // 使用纯 CUDA 原生的寄存器打包方式
            uint32_t packed;
            auto* p_packed = reinterpret_cast<uint16_t*>(&packed);
            p_packed[0] = reinterpret_cast<uint16_t&>(el_lo);
            p_packed[1] = reinterpret_cast<uint16_t&>(el_hi);

            // 执行一次 32-bit Shuffle
            uint32_t got_packed = __shfl_sync(0xffffffffu, packed, src_lane);

            // 解包并选择
            uint16_t res_bits = select_hi ? (got_packed >> 16) : (got_packed & 0xFFFF);
            tOrP_copy_view(j) = reinterpret_cast<Element&>(res_bits);
        } 
        else {
            // 回退路径
            const float src_lo = static_cast<float>(rP(base_idx + 0));
            const float src_hi = static_cast<float>(rP(base_idx + 2));
            const float got_lo = __shfl_sync(0xffffffffu, src_lo, src_lane);
            const float got_hi = __shfl_sync(0xffffffffu, src_hi, src_lane);
            tOrP_copy_view(j) = static_cast<Element>(select_hi ? got_hi : got_lo);
        }
    }

    return tOrP;
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// For V100 (SM70), each thread already holds 8 elements (128-bit), 
// matching the Philox RNG's native vector width. 
// We return the layout as-is to process 8 elements at a time.
template<typename Layout>
__forceinline__ __device__ auto convert_layout_acc_dropout(Layout acc_layout) {
    // 纯血 V100 (SM70) 专用版
    static_assert(decltype(size<0>(acc_layout))::value == 8, "V100 requires 8 fragments per thread");
    static_assert(decltype(rank(acc_layout))::value == 3, "Layout must be 3D");
    
    // V100 算完 QK^T 后每个线程正好持有 8 个元素
    // 完美契合 Philox 引擎一次处理 8 个 fp16 (128-bit) 的需求，无需向 MMA_N 借位。
    return acc_layout;
};

////////////////////////////////////////////////////////////////////////////////////////////////////

template <typename To_type, typename Engine, typename Layout>
__forceinline__ __device__ auto convert_type(Tensor<Engine, Layout> const &tensor) {
    using From_type = typename Engine::value_type;
    constexpr int numel = decltype(size(tensor))::value;
    cutlass::NumericArrayConverter<To_type, From_type, numel> convert_op;
    // HACK: this requires tensor to be "contiguous"
    auto frag = convert_op(*reinterpret_cast<const cutlass::Array<From_type, numel> *>(tensor.data()));
    return make_tensor(make_rmem_ptr<To_type>(&frag), tensor.layout());
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <typename To_type, typename Engine, typename Layout>
__forceinline__ __device__ auto convert_type_array(Tensor<Engine, Layout> const &tensor) {
    using From_type = typename Engine::value_type;
    constexpr int numel = decltype(size(tensor))::value;
    cutlass::NumericArrayConverter<To_type, From_type, numel> convert_op;
    // Return the converted fragment by value so the caller owns the backing storage.
    return convert_op(*reinterpret_cast<const cutlass::Array<From_type, numel> *>(tensor.data()));
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <typename To_type, int N, typename Layout>
__forceinline__ __device__ auto make_tensor_from_array(cutlass::Array<To_type, N> &storage, Layout const &layout) {
    return make_tensor(make_rmem_ptr<To_type>(storage.data()), layout);
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <typename Engine, typename Layout>
__forceinline__ __device__ void relu_(Tensor<Engine, Layout> &tensor) {
    constexpr int numel = decltype(size(tensor))::value;
    static_assert(numel % 2 == 0);
    using value_t = typename Engine::value_type;
    // HACK: this requires tensor to be "contiguous"
    Tensor tensor_uint32 = recast<uint32_t>(tensor);
    #pragma unroll
    for (int i = 0; i < size(tensor_uint32); ++i) {
        tensor_uint32(i) = relu2<value_t>(tensor_uint32(i));
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// On SM80 and above, we can fuse fp32 -> fp16/bf16 conversion and relu into 1 instruction
template <typename To_type, typename Engine, typename Layout>
__forceinline__ __device__ auto convert_type_relu(Tensor<Engine, Layout> const &tensor) {
    using From_type = typename Engine::value_type;
    static_assert(std::is_same_v<To_type, cutlass::half_t> || std::is_same_v<To_type, cutlass::bfloat16_t>);
    static_assert(std::is_same_v<float, From_type>);
    constexpr int numel = decltype(size(tensor))::value;
    static_assert(numel % 2 == 0);
#if defined(__CUDA_ARCH__) && __CUDA_ARCH__ >= 800
    // HACK: this requires tensor to be "contiguous"
    Tensor tensor_float2 = recast<float2>(tensor);
    Tensor out_uint32 = make_tensor<uint32_t>(tensor_float2.layout());
    #pragma unroll
    for (int i = 0; i < size(out_uint32); ++i) {
        out_uint32(i) = convert_relu2<To_type>(tensor_float2(i));
    }
    Tensor out = make_tensor(make_rmem_ptr<To_type>(out_uint32.data()), tensor.layout());
#else
    Tensor out = FLASH_NAMESPACE::convert_type<To_type>(tensor);
    FLASH_NAMESPACE::relu_(out);
#endif
    return out;
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// Blocks until all but N previous cp.async.commit_group operations have committed.
// This differs from cute::cp_async_wait in that when N = 0 we don't call cp.async.wait_all
// (which is equivalent to commit_group then wait_group 0).
// Instead we just call cp.async.wait_group 0, which is slightly faster.
// https://github.com/NVIDIA/cutlass/blob/master/include/cute/arch/copy_sm80.hpp#L113
template <int N>
CUTE_HOST_DEVICE
void cp_async_wait() {
#if defined(CUTE_ARCH_CP_ASYNC_SM80_ENABLED)
    asm volatile("cp.async.wait_group %0;\n" :: "n"(N));
#endif
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// resolves offset of a slice of a paged kv copy from gmem.
// assumes that the tensor has already been positioned at the correct head.
template <typename Kernel_traits>
__forceinline__ __device__
int64_t resolve_thread_kv_page_slice_offset(
    const int tidx, const int n_block, const int page_block_size, 
    const int* block_table, const int page_stride, const int row_stride,
    std::optional<int> partial_block_size = std::nullopt
) {
    constexpr int kGmemThreadsPerRow = Kernel_traits::kGmemThreadsPerRow;
    constexpr int kGmemRowsPerThread = Kernel_traits::kGmemRowsPerThread;
    constexpr int kGmemElemsPerLoad = Kernel_traits::kGmemElemsPerLoad;
    constexpr int kBlockN = Kernel_traits::kBlockN;
    
    const int64_t col_offset = tidx % kGmemThreadsPerRow * kGmemElemsPerLoad;
    int64_t block_row_offset = tidx / kGmemThreadsPerRow * kGmemRowsPerThread;

    if (partial_block_size) {
        // if we have a partial block, we need to adjust the row offset to avoid
        // reading of the end end of the block_table
        // get the offset of the last row in the kBlockN we care about
        auto final_row_offset = std::max(*partial_block_size - 1, 0);
        // adjust the row offset to account for each thread loading multiple
        // rows
        auto final_thread_row_offset = 
          ceil_div(final_row_offset, kGmemRowsPerThread) * kGmemRowsPerThread;
        block_row_offset = std::min(
            block_row_offset, int64_t(final_thread_row_offset));
    }

    const int64_t global_row_offset = block_row_offset + n_block * kBlockN;
    const int64_t page_offset = global_row_offset % page_block_size;
    const int64_t virtual_page_idx = global_row_offset / page_block_size;

    return ((int64_t) block_table[virtual_page_idx]) * ((int64_t) page_stride)
        + page_offset * ((int64_t) row_stride)
        + col_offset;
}

////////////////////////////////////////////////////////////////////////////////////////////////////

// Layout reshape function. Given a layout with modes ((v1, v2), m, k), returns (v1, v2, k),         
// where v2 may be a tuple itself, in the case of swizzled smem-backed thread tiles. This ensures
// that paged and non-paged copies result in equivalently shaped, if not necessarily strided, tensors.
template <class Shape, class Stride>
__forceinline__ __device__
auto reshape_thread_tile(Layout<Shape, Stride> l) {
    return make_layout(append(get<0>(l.shape()), get<2>(l.shape())),
                        append(get<0>(l.stride()), get<2>(l.stride())));
}

// reshapes and flattens the thread tile layout. A separate function is needed for the case where
// one of the modes of l is a layout itself and must be flattened, as opposed to keeping it intact
// for the case of swizzled layouts
template <class Shape, class Stride>
__forceinline__ __device__
auto reshape_flatten_thread_tile(Layout<Shape, Stride> l) {
    auto mode_0 = filter(flatten(get<0>(l)));
    return make_layout(append(mode_0.shape(), get<2>(l.shape())),
                        append(mode_0.stride(), get<2>(l.stride())));
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <bool Is_even_MN=true, bool Is_even_K=true, bool Clear_OOB_MN=false, bool Clear_OOB_K=true,
          typename TiledCopy, typename Engine0, typename Layout0, typename Engine1, typename Layout1,
          typename Engine2, typename Layout2, typename Engine3, typename Layout3>
__forceinline__ __device__ void copy(TiledCopy tiled_copy, Tensor<Engine0, Layout0> const &S,
                            Tensor<Engine1, Layout1> &D, Tensor<Engine2, Layout2> const &identity_MN,
                            Tensor<Engine3, Layout3> const &predicate_K, const int max_MN=0) {
    CUTE_STATIC_ASSERT_V(rank(S) == Int<3>{});
    CUTE_STATIC_ASSERT_V(rank(D) == Int<3>{});
    CUTE_STATIC_ASSERT_V(size<0>(S) == size<0>(D));                     // MMA
    CUTE_STATIC_ASSERT_V(size<1>(S) == size<1>(D));                     // MMA_M
    CUTE_STATIC_ASSERT_V(size<2>(S) == size<2>(D));                     // MMA_K
    // There's no case where !Clear_OOB_K && Clear_OOB_MN
    static_assert(!(Clear_OOB_MN && !Clear_OOB_K));
    #pragma unroll
    for (int m = 0; m < size<1>(S); ++m) {
        if (Is_even_MN || get<0>(identity_MN(0, m, 0)) < max_MN) {
            #pragma unroll
            for (int k = 0; k < size<2>(S); ++k) {
                if (Is_even_K || predicate_K(k)) {
                    cute::copy(tiled_copy, S(_, m, k), D(_, m, k));
                } else if (Clear_OOB_K) {
                    cute::clear(D(_, m, k));
                }
            }
        } else if (Clear_OOB_MN) {
            cute::clear(D(_, m, _));
        }
    }
    // TD [2023-04-13]: Strange that the code below can cause race condition.
    // I think it's because the copies are under an if statement.
    // if (Is_even_K) {
    //     #pragma unroll
    //     for (int m = 0; m < size<1>(S); ++m) {
    //         if (Is_even_MN || get<0>(identity_MN(0, m, 0)) < max_MN) {
    //             copy(tiled_copy, S(_, m, _), D(_, m, _));
    //         } else if (Clear_OOB_MN) {
    //             clear(D(_, m, _));
    //         }
    //     }
    // } else {  // It's slightly faster in this case if iterate over K first
    //     #pragma unroll
    //     for (int k = 0; k < size<2>(S); ++k) {
    //         if (predicate_K(k)) {
    //             #pragma unroll
    //             for (int m = 0; m < size<1>(S); ++m) {
    //                 if (Is_even_MN || get<0>(identity_MN(0, m, 0)) < max_MN) {
    //                     copy(tiled_copy, S(_, m, k), D(_, m, k));
    //                 } else if (Clear_OOB_MN) {
    //                     clear(D(_, m, k));
    //                 }
    //             }
    //         } else if (Clear_OOB_K) {  // There's no case where !Clear_OOB_K && Clear_OOB_MN
    //             if (Clear_OOB_MN || Is_even_MN) {
    //                 clear(D(_, _, k));
    //             } else {
    //                 #pragma unroll
    //                 for (int m = 0; m < size<1>(S); ++m) {
    //                     if (!(Is_even_MN || get<0>(identity_MN(0, m, 0)) < max_MN)) {
    //                         clear(D(_, m, k));
    //                     }
    //                 }
    //             }
    //         }
    //     }
    // }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <bool Is_even_K=true,
          typename Engine0, typename Layout0, typename Engine1, typename Layout1,
          typename Engine2, typename Layout2, typename Engine3, typename Layout3>
__forceinline__ __device__ void copy_w_min_idx(Tensor<Engine0, Layout0> const &S,
                                      Tensor<Engine1, Layout1> &D, Tensor<Engine2, Layout2> const &identity_MN,
                                      Tensor<Engine3, Layout3> const &predicate_K,
                                      const int max_MN=0, const int min_MN=0) {
    CUTE_STATIC_ASSERT_V(rank(S) == Int<3>{});
    CUTE_STATIC_ASSERT_V(rank(D) == Int<3>{});
    CUTE_STATIC_ASSERT_V(size<0>(S) == size<0>(D));                     // MMA
    CUTE_STATIC_ASSERT_V(size<1>(S) == size<1>(D));                     // MMA_M
    CUTE_STATIC_ASSERT_V(size<2>(S) == size<2>(D));                     // MMA_K
    // if (threadIdx.x == 0 && blockIdx.z == 0) { printf("blockIdx.y = %d, max_MN = %d, min_MN = %d\n", blockIdx.y, max_MN, min_MN); }
    #pragma unroll
    for (int m = 0; m < size<1>(S); ++m) {
        // if (threadIdx.x == 0 && blockIdx.z == 0) { printf("blockIdx.y = %d, m = %d\n", blockIdx.y, get<0>(identity_MN(0, m, 0))); }
        if (get<0>(identity_MN(0, m, 0)) >= min_MN && get<0>(identity_MN(0, m, 0)) < max_MN) {
            // if (threadIdx.x == 0 && blockIdx.z == 0) { printf("Inner loop, blockIdx.y = %d, m = %d\n", blockIdx.y, get<0>(identity_MN(0, m, 0))); }
            #pragma unroll
            for (int k = 0; k < size<2>(S); ++k) {
                if (Is_even_K || predicate_K(k)) {
                    cute::copy(S(_, m, k), D(_, m, k));
                }
            }
        }
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

template <typename Engine, typename Layout>
__forceinline__ __device__ void apply_softcap(Tensor<Engine, Layout> &tensor, const float softcap){
    #pragma unroll
    for (int i = 0; i < size(tensor); ++i) {
        tensor(i) = cutlass::fast_tanh(tensor(i) * softcap);
    }
}

template <typename Engine0, typename Layout0, typename Engine1, typename Layout1>
__forceinline__ __device__ void calculate_dtanh(Tensor<Engine0, Layout0> &src_tensor, Tensor<Engine1, Layout1> &dst_tensor, const float softcap){
    #pragma unroll
    for (int i = 0; i < size(src_tensor); ++i) {
        dst_tensor(i) = (1.f - (src_tensor(i) * src_tensor(i))) * softcap;
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////

}  // namespace FLASH_NAMESPACE
